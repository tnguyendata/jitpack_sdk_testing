plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def extractSdkVersion() {
    def systemFile = file('src/main/java/com/datalogic/device/info/SYSTEM.java')
    def majorVersion = null
    def minorVersion = null
    
    systemFile.eachLine { line ->
        if (line.contains('SDK_VERSION_MAJOR') && line.contains('=')) {
            def matcher = (line =~ /=\s*(\d+)/)
            if (matcher.find()) {
                majorVersion = matcher[0][1]
            }
        }
        if (line.contains('SDK_VERSION_MINOR') && line.contains('=')) {
            def matcher = (line =~ /=\s*(\d+)/)
            if (matcher.find()) {
                minorVersion = matcher[0][1]
            }
        }
    }
    
    if (majorVersion == null || minorVersion == null) {
        throw new GradleException("Could not extract SDK version from SYSTEM.java")
    }
    
    return "v${majorVersion}.${minorVersion}"
}

def String GROUP = 'com.github.datalogic'
def String ARTIFACT_ID = 'datalogic-android-sdk'
def String SDK_VERSION = extractSdkVersion()

android {
    compileSdkVersion 35
    namespace 'com.datalogic.android.sdk'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 35
        versionCode 1
        versionName "1.0"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

publishing {
    publications {
        release(MavenPublication) {
            groupId = GROUP
            artifactId = ARTIFACT_ID
            version = SDK_VERSION

            pom {
                name = 'Datalogic Android SDK'
                description = 'Official SDK for Datalogic mobile devices on Android.'
                url = 'https://github.com/datalogic/datalogic-android-sdk'
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release {
                artifact(bundleReleaseAar)
            }
        }
    }
}